{
  globalNixosModules ? [ ],
  globalHomeModules ? [ ],
}:
{
  inputs,
  lib,
  ...
}:
let
  # Import paths configuration
  paths = import ./paths.nix inputs.self.outPath;

  # Import utility modules
  fs = import ./fs.nix { inherit lib; };
  packagesLib = import ./packages.nix { inherit inputs fs; };
  modulesLib = import ./modules.nix { inherit fs; };
  templatesLib = import ./templates.nix { inherit fs; };
  usersLib = import ./users.nix { inherit lib; };
  shellsLib = import ./shells.nix { inherit lib fs paths; };

  # Discover all components
  hostToUsersMap = usersLib.getUserHostMap paths.home;
  discoveredSystemModules = modulesLib.mkModules paths.systemModules;
  discoveredHomeModules = modulesLib.mkModules paths.homeModules;
  discoveredTemplates = templatesLib.mkTemplates paths.templates;

  # Import nixos configuration generator
  nixosLib = import ./nixos.nix {
    inherit
      inputs
      lib
      fs
      paths
      hostToUsersMap
      discoveredSystemModules
      discoveredHomeModules
      globalNixosModules
      globalHomeModules
      ;
  };

  # Import home-manager configuration generator
  homeLib = import ./home.nix {
    inherit
      inputs
      lib
      paths
      hostToUsersMap
      discoveredHomeModules
      globalHomeModules
      ;
  };

  # Import deploy-rs configuration generator
  deployLib = import ./deploy.nix {
    inherit inputs paths fs;
  };
in
{
  perSystem =
    {
      pkgs,
      system,
      ...
    }:
    {
      packages = packagesLib.mkPackages paths.packages system;
      devShells = shellsLib.mkShells { inherit pkgs inputs; };
    };

  flake = {
    nixosModules = discoveredSystemModules;
    homeModules = discoveredHomeModules;
    nixosConfigurations = nixosLib.hosts;
    nixosContainers = nixosLib.containers;
    homeConfigurations = homeLib.mkHomeConfigurations;
    templates = discoveredTemplates;
    overlays.default =
      final: prev: packagesLib.mkPackages paths.packages final.stdenv.hostPlatform.system;
    deploy.nodes = deployLib.mkDeployNodes;
  };
}
